%let API_KEY=<YOUR API KEY>;
%let API_SECRET=<YOUR API SECRET>;

filename head temp;
filename body temp;
filename resp temp;

data _null_;
   file head lrecl=2048;
   auth = put( catx(':' , symget('API_KEY') , symget('API_SECRET')) , $base64x32767.);
   put 'Authorization: Basic ' auth;
run;

data _null_;
   file body;
   put 'grant_type=client_credentials&';
run;

proc http
   url="https://api.twitter.com/oauth2/token?"
   method="post"
   ct="application/x-www-form-urlencoded;charset=UTF-8"
   in=body
   headerin=head
   out=resp;
run;

 
data _null_;
   infile resp dlm='"';
   input @'access_token' + 3 bearer_token : $1024.;
   call symputx('bearer_token' , bearer_token);
run;
